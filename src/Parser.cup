import java_cup.runtime.*;

parser code  {:
  public boolean syntaxErrors = false;
  public void syntax_error(Symbol current_token) {
    report_error(
    "Syntax error at line " + (current_token.left + 1) + ", column " + current_token.right + "\n", null);
    syntaxErrors = true;
  }
  public void log(String logStr) {
      if (true) System.out.println(logStr);
  }
:};

// *************************************
// ===== TERMINALS & NON-TERMINALS =====
// *************************************

// Control flow
terminal IF, THEN, ELSE, ENDIF;
terminal LOOP, ENDLOOP;
terminal BREAK;
terminal RETURN;

// Entry point
terminal MAIN;

// Type definitions
terminal DEFINE_FUNCTION; // fdef
terminal DEFINE_TYPE;     // tdef
terminal ALIAS;

// Primitive types
terminal BOOLEAN;         // Boolean type
terminal INT, RAT, FLOAT; // Numeric types
terminal CHAR;            // Character type (single)
terminal DICT;            // Dictionary type
terminal SEQ;             // List type
terminal TOP;             // Any type

// Input / output
terminal READ;
terminal PRINT;

// Separators
terminal LPAREN, RPAREN; // ()
terminal LBRACE, RBRACE; // {}
terminal LBRACK, RBRACK; // []
terminal LANGLE, RANGLE; // <>

terminal SEMICOLON;
terminal COLON;
terminal COMMA;
terminal DOT;

/* OPERATORS */

// Boolean operators
terminal NOT;      // !
terminal AND;      // &&
terminal OR;       // ||
terminal IMPLIES;  // =>

// Numeric operators
terminal PLUS, MINUS, MULT, DIV, POF; // +, -, *, /, ^
terminal EQEQ, NOTEQ, LTEQ;           // =, !=, <= (comparison)

// Language constructs
terminal EQ;                          // := (assignment)
terminal IN;                          // 'in' - Dictionary and seq in
terminal CONCAT;                      // :: (concat for dictionaries)
terminal QUESTION;                    // ? (surrounds predicate for function calls)

// Literals
terminal java.lang.Number    INTEGER_LITERAL;
terminal java.lang.Number    FLOAT_LITERAL;
terminal java.lang.Boolean   BOOLEAN_LITERAL;
terminal java.lang.Character CHARACTER_LITERAL;
terminal java.lang.String    STRING_LITERAL;

terminal                     RATIONAL_LITERAL;
terminal                     NULL_LITERAL;

terminal java.lang.String    IDENTIFIER;


/* Non-terminals */

// Program Structure
nonterminal program;
nonterminal main_statement;
nonterminal global_declarations, global_declaration, global_declarations_opt;
nonterminal declarations, declaration;

// Lexical Structure.
nonterminal literal;

// Data types
nonterminal type, primitive_type, aggregrate_type, reference_type;
nonterminal numeric_type, integral_type;
nonterminal dict_type, seq_type;

// Operators
nonterminal unary_operator, binary_operator, assignment_operator;

// Blocks and Statements
nonterminal block, block_statements_opt, block_statements, block_statement;
nonterminal block_body, block_body_loop, block_body_opt;
nonterminal statement, statement_without_trailing_substatement;
nonterminal print_statement, read_statement;
nonterminal alias_statement;
nonterminal variable_declaration_statement;

// Blocks & Statements for Loops
nonterminal block_statements_loop;
nonterminal statement_loop;

// Control Flow Statements
nonterminal if_then_statement, if_then_else_statement;
nonterminal loop_statement;
nonterminal return_statement;
nonterminal break_statement;

// Control Flow for Loops
nonterminal if_then_loop_statement, if_then_else_loop_statement, loop_body;

// Expressions
nonterminal expression, subexpression, expression_statement;
nonterminal assignment_statement;
nonterminal field_access;
nonterminal seq_access, seq_access_lhs;

// Variable declaration & Initializers
nonterminal variable_declaration;
nonterminal expression_list;
nonterminal dict_initializer, seq_initializer;
nonterminal key_value_pairs, key_value_pair;

// Function declaration (fdef)
nonterminal define_function_statement;
nonterminal function_header, function_body, function_datatype_opt;
nonterminal formal_parameter_list_opt, formal_parameter_list, formal_parameter;

// Function invocation
nonterminal function_invocation, function_condition;
nonterminal function_arguments_opt, function_arguments, function_argument;

// Struct declaration (tdef) and alias
nonterminal define_struct_statement, struct_body;
nonterminal struct_fields_opt; // For empty structs with no fields
nonterminal struct_fields, struct_field;

// Precedence rules
precedence left PLUS, MINUS;
precedence left DIV, MULT;

// *************************************
// ========= PRODUCTION RULES ==========
// *************************************

start with program;

// Program structure
program ::=
      global_declarations_opt main_statement global_declarations_opt;
      
      // main_statement
    // | main_statement statements
    // | statements main_statement
    // | statements main_statement statements;
    
global_declarations_opt ::=
    | global_declarations;
    
global_declarations ::=
      global_declaration global_declarations
    | global_declaration;
    
global_declaration ::=
      variable_declaration_statement
    | define_function_statement
    | define_struct_statement
    | alias_statement;
    
// declarations ::=
      // declarations declaration
    // | declaration;
    
// declaration ::=
      // variable_declaration_statement
    // ;

main_statement ::= 
      MAIN block SEMICOLON;

// Lexical Structure.
literal ::= 
      INTEGER_LITERAL
    | FLOAT_LITERAL
    | BOOLEAN_LITERAL
    | CHARACTER_LITERAL
    | STRING_LITERAL
    | RATIONAL_LITERAL
    | NULL_LITERAL;

// Data types
type ::= 
      primitive_type
    | aggregrate_type
  	| reference_type;	// struct

primitive_type ::= 
      numeric_type
    | BOOLEAN
    | TOP;

numeric_type ::= 
      integral_type
  	| RAT
    | FLOAT;

integral_type ::= 
      INT
    | CHAR;

aggregrate_type ::= 
      dict_type
    | seq_type;

reference_type ::=
      IDENTIFIER;

dict_type ::= 
      DICT LANGLE type COMMA type RANGLE;

seq_type ::=
      SEQ LANGLE type RANGLE;

// Operators
unary_operator ::=
      NOT
    | MINUS;

binary_operator ::=
      PLUS
  	| MINUS
  	| MULT
	| DIV
    | POF
  	| AND
  	| OR
  	| IMPLIES
  	| IN
  	| CONCAT
  	| LANGLE
  	| EQEQ
  	| NOTEQ
  	| LTEQ;
    
assignment_operator ::=
      EQ;

// Blocks and Statements
// General structures (i.e. building blocks for other nonterminals)
// { ..statements.. }
block ::= 
      LBRACE block_body RBRACE;
      
block_body ::=
      declarations block_statements
    | block_statements;
      
block_body_opt ::=
    | block_body;
      
block_body_loop ::=
      declarations block_statements_loop
    | block_statements_loop;

// ..statements.. (zero or more)
block_statements_opt ::= 
    | block_statements;

block_statements ::= 
      block_statement
    | block_statements block_statement;
    
block_statements_loop ::= 
      statement_loop
    | block_statements_loop statement_loop;

block_statement ::= 
      return_statement
    | statement;

// TODO: add all statement types
statement ::= 
      statement_without_trailing_substatement
    | if_then_statement
    | if_then_else_statement
    | loop_statement;
    
statement_loop ::= 
      statement_without_trailing_substatement
    | break_statement
    | if_then_loop_statement
    | if_then_else_loop_statement
    | loop_statement;

// NOTE: Check if block should be present here or not. Block includes curly braces {}.
// These are for "simple" statements.
statement_without_trailing_substatement ::= 
      block
    | expression_statement
    | assignment_statement
    | print_statement
    | variable_declaration_statement
    | define_struct_statement
    | alias_statement
    | read_statement;
  	// | alias_statement
    // | return_statement;
  	// | define_struct_statement
  	// | define_function_statement;
    
assignment_statement ::=
      expression assignment_operator expression SEMICOLON;

print_statement ::= 
      PRINT expression SEMICOLON;

read_statement ::= 
      READ expression SEMICOLON;

alias_statement ::=
  	  ALIAS type IDENTIFIER SEMICOLON;

variable_declaration_statement ::= 
      variable_declaration SEMICOLON;

variable_declaration ::= 
      IDENTIFIER COLON type
    | IDENTIFIER COLON type EQ expression_list;

// Initializers
// variable_initializer ::=
      // expression;

expression_list ::=
      expression
    | expression_list COMMA expression;

dict_initializer ::= 
      LBRACE RBRACE
    | LBRACE key_value_pairs RBRACE;

key_value_pairs ::=
  	  key_value_pair
  	| key_value_pairs COMMA key_value_pair;

key_value_pair ::=
  	  IDENTIFIER COLON expression
  	| literal COLON expression;

seq_initializer ::=
  	  LBRACK RBRACK
    | LBRACK expression_list RBRACK;

// Control Flow
if_then_statement ::=
      IF LPAREN expression RPAREN THEN block_body ENDIF;

if_then_else_statement ::=
      IF LPAREN expression RPAREN THEN block_body ELSE block_body ENDIF;

// For loops
if_then_loop_statement ::=
      IF LPAREN expression RPAREN THEN loop_body ENDIF;

if_then_else_loop_statement ::=
      IF LPAREN expression RPAREN THEN loop_body ELSE loop_body ENDIF;

// Not clear if break needs to be the last statement in a loop - we're going to allow it.
loop_body ::=
      block_body
    | block_body break_statement block_statements_opt
    | break_statement block_statements_opt;

loop_statement ::=
      LOOP block_body_loop ENDLOOP;

return_statement ::=
      RETURN expression SEMICOLON
  	| RETURN SEMICOLON;

// Break accepts an optional integer to specify number of nested loops to break out of.
// We are making this an expression so that a variable can be evaluated for this value.
// Type checking to ensure an integer value will have to be performed during semantic analysis.
break_statement ::=
      BREAK expression SEMICOLON
  	| BREAK SEMICOLON;
    
// Expressions
expression_statement ::=
      expression SEMICOLON;

expression ::=
      expression binary_operator subexpression
    | subexpression;

subexpression ::=
      literal
    | IDENTIFIER
  	| field_access
  	| seq_access
    | seq_initializer
    | dict_initializer
  	| function_invocation
  	| unary_operator subexpression
    | LPAREN expression RPAREN;
    
field_access ::=
  	  seq_access_lhs DOT IDENTIFIER;
      
// The left-hand side of an array access.
// i.e. myVar[3], or myVar[3][2], or myVar.myField[2], or (getVar())[5]
seq_access_lhs ::=
      IDENTIFIER
    | seq_access
    | field_access
    | function_invocation
    | LPAREN expression RPAREN;

// NOTE: Identifier OR another array(seq)!
seq_access ::=
      seq_access_lhs LBRACK expression RBRACK
    | seq_access_lhs LBRACK expression COLON expression RBRACK
    | seq_access_lhs LBRACK expression COLON RBRACK
    | seq_access_lhs LBRACK COLON expression RBRACK;


// Function Definition
define_function_statement ::=
  		function_header function_body function_datatype_opt SEMICOLON;

function_header ::=
  		DEFINE_FUNCTION IDENTIFIER LPAREN formal_parameter_list_opt RPAREN;

formal_parameter_list_opt ::=
    | formal_parameter_list;

formal_parameter_list ::=
  		formal_parameter
    | formal_parameter_list COMMA formal_parameter;

formal_parameter ::=
  		IDENTIFIER COLON type;

function_body ::= 
  		block;

function_datatype_opt ::=
    | COLON type;

// Function invocation  
function_invocation ::= 
  		QUESTION function_condition QUESTION IDENTIFIER LPAREN function_arguments_opt RPAREN;

function_condition ::= 
  		expression;
  
function_arguments_opt ::=
    | function_arguments;

function_arguments ::=
  	  function_argument
  	| function_arguments COMMA function_argument;

/** Could be more **/
function_argument ::=
  	  expression;

// User-defined types
// Struct declaration
define_struct_statement ::= 
  	  DEFINE_TYPE IDENTIFIER struct_body SEMICOLON;

struct_body ::=
  	  LBRACE struct_fields_opt RBRACE;

struct_fields_opt ::= 
    | struct_fields;

struct_fields ::=
  	  struct_field
    | struct_fields COMMA struct_field;

struct_field ::= 
  	  IDENTIFIER COLON type; 